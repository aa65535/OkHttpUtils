buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

group = 'utils.okhttp'
version = '2.0.2'

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
sourceCompatibility = 1.7
targetCompatibility = 1.7

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    url 'https://github.com/aa65535/okhttputils'
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'aa65535'
            name 'Jian Chang'
            email 'aa65535@live.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Java utils library for Okhttp')
                root.children().last() + pomConfig
            }
        }
    }
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
}

bintray {
    if (new File('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
    } else {
        user = System.getenv("bintray.user")
        key = System.getenv("bintray.apikey")
    }
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'okhttputils'
        desc = 'A Java utils library for Okhttp'
        websiteUrl = 'https://github.com/aa65535/okhttputils'
        issueTrackerUrl = 'https://github.com/aa65535/okhttputils/issues'
        vcsUrl = 'https://github.com/aa65535/okhttputils.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
