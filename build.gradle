plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

group = 'utils.okhttp'
version = '2.4.9'

apply plugin: 'java'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    testImplementation 'com.google.code.gson:gson:2.8.5'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task createJavadoc(type: Javadoc) {
    options.encoding = "utf-8"
}

task javadocJar(type: Jar, dependsOn: createJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    url 'https://github.com/aa65535/okhttputils'
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'aa65535'
            name 'Jian Chang'
            email 'aa65535@live.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId
            artifactId 'okhttputils'
            version this.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Java utils library for Okhttp')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        user = properties.getProperty('bintray.user')
        key = properties.getProperty('bintray.apikey')
    } else {
        user = System.getenv('bintray.user')
        key = System.getenv('bintray.apikey')
    }
    publications = ['mavenJava']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = 'okhttputils'
        desc = 'A Java utils library for Okhttp'
        publicDownloadNumbers = true
        websiteUrl = 'https://github.com/aa65535/okhttputils'
        issueTrackerUrl = 'https://github.com/aa65535/okhttputils/issues'
        vcsUrl = 'https://github.com/aa65535/okhttputils.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = this.version
            desc = "release v${this.version}"
            released = new Date()
            vcsTag = "v${this.version}"
        }
    }
}

wrapper {
    gradleVersion = '5.1.1'
}
